buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
}

// FORGE
apply plugin: 'net.minecraftforge.gradle.forge'

// MAVEN PUBLISHING
apply plugin: 'maven'

dependencies {
//    compile 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
    compile 'org.apache.httpcomponents:httpclient:4.5.13'

    // https://mvnrepository.com/artifact/commons-io/commons-io
    compile group: 'commons-io', name: 'commons-io', version: '2.6'

    compileOnly 'org.projectlombok:lombok:1.18.16'
    annotationProcessor 'org.projectlombok:lombok:1.18.16'

    testCompileOnly 'org.projectlombok:lombok:1.18.16'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.16'

    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
}

group = "de.labyhelp.addon" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
version = "2.5.6"
archivesBaseName = "LabyHelp"

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/labyhelp/labyhelp")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            artifactId = "labyhelp"
            from(components.java)
            pom {
                name = 'laby-help'
                description = 'The offical API for the LabyHelp addon'
                url = 'http://www.labyhelp.de'
                developers {
                    developer {
                        id = 'marvinhuelsmann'
                        name = 'Marvin Huelsmann'
                        url = 'https://marvhuelsmann.de'
                    }
                    developer {
                        id = 'rufusmaiwald'
                        name = 'Rufus Maiwald'
                        url = 'https://rufusmaiwald.de'
                    }
                    developer {
                        id = 'dominklippl'
                        name = 'Dominik Lippl'
                        url = 'https://dominiklippl.de'
                    }
                }
            }
        }
    }
}

minecraft {
    version = "1.8.9-11.15.1.1855"
    runDir = "run"
    mappings = "stable_20"
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

compileJava.options.encoding = 'UTF-8'

jar {
    manifest {
        attributes 'Main-Class': 'de.labyhelp.addon.Main'
    }
}

reobf {
    jar {
        useNotchSrg()
    }
}
